package config;
    dialect "java"

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;
import com.travel_recommender.opta.Day;
import com.travel_recommender.opta.Spot;
import com.travel_recommender.opta.Solution;
import com.travel_recommender.opta.SpotSnippet;
import com.travel_recommender.opta.TimeCapsule
import com.travel_recommender.opta.SpotConsecutiveStart
import com.travel_recommender.opta.SpotConsecutiveEnd
import com.travel_recommender.opta.SpotConsecutiveSequence
import com.travel_recommender.opta.SpotFullyArranged
import com.travel_recommender.opta.TimeCapsuleAlreadyAssigned;

global HardSoftScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "One TimeCapsule to One Spotsnippet"
    salience 3
    when
        $onecapsule : TimeCapsule()
        $onesnippet : SpotSnippet($opid : id, timeCapsule == $onecapsule)
        $conflictNum : Number() from accumulate (
            SpotSnippet(id != $opid, timeCapsule == $onecapsule, $score : spot.getScore()),
            sum($score)
        )
    then
        scoreHolder.addHardConstraintMatch(kcontext, - 10 * $conflictNum.intValue());
end

rule "Reward SpotSnippet Consecutive 2 Arrangement"
	when
		$leftcap : TimeCapsule($lcid : id)
		$rightcap : TimeCapsule(id == $lcid + 1)
		Day(timeCapsules contains $leftcap, timeCapsules contains $rightcap)
		$leftsnippet : SpotSnippet($spot : spot, timeCapsule == $leftcap, $ssid : snippetIndexInSpot)
		$rightsnippet : SpotSnippet(spot == $spot, timeCapsule == $rightcap, snippetIndexInSpot == $ssid + 1)
	then
		System.out.println("Rewarding the arrange :" + $leftsnippet.getId() + "@" + $lcid +" - " + $rightsnippet.getId() + "@" + ($lcid + 1));
		scoreHolder.addHardConstraintMatch(kcontext, 1);
end

rule "No visit Accross Days"
    when
        $leftcap : TimeCapsule($lcid : id)
        $rightcap : TimeCapsule($rcid : id)
        $leftday : Day(timeCapsules contains $leftcap, $ldid : id)
        $rightday : Day(timeCapsules contains $rightcap, id != $ldid)
        $spot : Spot()
        $leftsnippet : SpotSnippet(spot == $spot, timeCapsule == $leftcap)
        $rightsnippet : SpotSnippet(spot == $spot, timeCapsule == $rightcap)
        //SpotConsecutiveSequence($spot : spot, $startcapsule : startCapsule, $endcapsule : endCapsule)
        //$day : Day(timeCapsules contains $startcapsule, $dayIndex : id)
        //Day(timeCapsules contains $endcapsule, id > $dayIndex)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end// todo: change the value of hardconstraint

rule "visitSpotOnce"
    salience 2
    when
        $leftcapsule : TimeCapsule($lcid : id)
        $rightcapsule : TimeCapsule($rcid : id > $lcid + 1)
        $leftsnippet : SpotSnippet($spot : spot, timeCapsule == $leftcapsule, $ssid : snippetIndexInSpot)
        $rightsnippet : SpotSnippet(spot == $spot, timeCapsule == $rightcapsule, snippetIndexInSpot == $ssid + 1)
    then
        scoreHolder.addHardConstraintMatch(kcontext, $lcid.intValue() - $rcid.intValue());
end

rule "Reserve Gourmet Spots Exclusive to Meal Time"
    when
        $spot : Spot($gourmet : gourmet)
        $timecapsule : TimeCapsule($cid : id)
        $day : Day(timeCapsules contains $timecapsule)
        SpotSnippet(spot == $spot,  timeCapsule == $timecapsule)
    then
        if ( $day.notMealTime($cid) == $gourmet ) {
            scoreHolder.addHardConstraintMatch(kcontext, -1);
        } else {
            scoreHolder.addSoftConstraintMatch(kcontext, 0);
        }
end

rule "Reserve Travel Time Between Spots"
    //salience 2
    when
        $leftcapsule : TimeCapsule($leftcapsuleId : id)
        $day : Day(timeCapsules contains $leftcapsule)
        $leftspot : Spot($sid : id)
        $rightspot : Spot(id != $sid)
        $leftsnippet : SpotSnippet( spot == $leftspot,
                                    timeCapsule == $leftcapsule)
        $othercapsule : TimeCapsule(id >= $leftcapsuleId)
        exists SpotSnippet( spot == $rightspot, snippetIndexInSpot == 0, timeCapsule == $othercapsule)
    then
        int travelcapsulenum = $rightspot.getTravelRequiredTimeCapNum($leftspot, $day);
        int capsulenum = $rightspot.getRequiredTimeCapsuleNum($day);
        if ($leftsnippet.getSnippetIndexInSpot() == capsulenum - 1 && $othercapsule.getId() - $leftcapsuleId < travelcapsulenum) {
			System.out.println("Punishing Insufficient traveltime: " + ($othercapsule.getId() - $leftcapsuleId) + ", need " + travelcapsulenum + " capsules");
            scoreHolder.addHardConstraintMatch(kcontext, $othercapsule.getId().intValue() - $leftcapsuleId.intValue() - travelcapsulenum);
        } else {
            scoreHolder.addSoftConstraintMatch(kcontext, 0);
        }
end

rule "No Exceed Time of Spot"
    //salience 1
    when
        $day : Day($timecapsules : timeCapsules)
        $totalScheduledCaps : Number() from accumulate (
            SpotSnippet(timeCapsule != null, $capsuleCost : getScore(), $timecapsules contains timeCapsule),
            sum($capsuleCost)
        )
    then
        int timeCapsuleTotNum = $day.getRelativeTimeCapsuleIndex($day.getEnd_time());
        if (timeCapsuleTotNum < $totalScheduledCaps.intValue()) {
            scoreHolder.addHardConstraintMatch(kcontext, timeCapsuleTotNum - $totalScheduledCaps.intValue());
        }
end

rule "Visit Within Time Window of Spot"
    when
        $capsule : TimeCapsule($cid : id)
        $day : Day(timeCapsules contains $capsule)
        $spot : Spot()
        SpotSnippet(spot == $spot, timeCapsule == $capsule, $snippetIndexInSpot : snippetIndexInSpot)
    then
        int opencapId = $spot.getOpenTimeCapId($day);
        int closecapId = $spot.getCloseTimeCapId($day);
        int requriedcapnum = $spot.getRequiredTimeCapsuleNum($day);
        if (($snippetIndexInSpot == 0 && $cid < opencapId)||($snippetIndexInSpot == (requriedcapnum - 1) && $cid > closecapId)) {
            // System.out.println($cid);
            scoreHolder.addHardConstraintMatch(kcontext, -1);
        } else {
            scoreHolder.addSoftConstraintMatch(kcontext, 0);
        }
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "Arrange Spot to its Fullest"
    when
        $day : Day()
        $spot : Spot($id : id)
        $totalCapsuleNumOfSpot : Number() from accumulate (
            SpotSnippet(spot == $spot,
                        timeCapsule : timeCapsule != null,
                        $score : score,
                        $day.getTimeCapsules() contains timeCapsule),
            sum($score)
        )
    then
        int requiredTimeCapsuleNum = $spot.getRequiredTimeCapsuleNum($day);
        scoreHolder.addSoftConstraintMatch(kcontext, $totalCapsuleNumOfSpot.intValue() - requiredTimeCapsuleNum);
end

rule "Minimize Travel Time"
    when
        $leftcapsule : TimeCapsule($lcid : id)
        $rightcapsule : TimeCapsule($rcid : id != $lcid)
        $day : Day(timeCapsules contains $leftcapsule, timeCapsules contains $rightcapsule)
        $leftspot : Spot($lsid : id)
        $leftsnippet : SpotSnippet(spot == $leftspot, timeCapsule == $leftcapsule, $snippetIndexInSpot : snippetIndexInSpot)
        $rightspot : Spot(id != $lsid)
        exists SpotSnippet(spot == $rightspot, timeCapsule == $rightcapsule, snippetIndexInSpot == 0)
    then
        int capsulenum = $leftspot.getRequiredTimeCapsuleNum($day);
        int travelcapNum = $rightspot.getTravelRequiredTimeCapNum($leftspot, $day);
        if ($snippetIndexInSpot == (capsulenum - 1)) {
            scoreHolder.addSoftConstraintMatch(kcontext, - travelcapNum);
        }
        // System.out.println("Travel caps between " + $lsid + " and " + $rightspot.id + " = " + travelcapNum);
end

rule "Minimize unmatched SpotSnippet"
    when
        $totalUnmatchedSpotScore : Number() from accumulate (
            SpotSnippet($spotScore : getScore(), timeCapsule == null),
            sum($spotScore)
        )
    then
        // System.out.println("Total unmatched Spot Snippets: " + $totalUnmatchedSpotScore);
        scoreHolder.addSoftConstraintMatch(kcontext, - $totalUnmatchedSpotScore.intValue());
end
/*
rule "Compact Schedule Earlier"
    when
        $timeCapsule : TimeCapsule()
        $day : Day($did : id, timeCapsules contains $timeCapsule)
        $nextday : Day(id > $did)
        SpotSnippet(timecapsules == $timeCapsule)
        not SpotSnippet(timeCapsule.getId())
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $did.intValue());
end
*/
